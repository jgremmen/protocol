plugins {
  id 'antlr'
  id "org.sonarqube" version "3.0"
  id 'io.franzbecker.gradle-lombok' version '5.0.0'
}


description 'Protocol Library Message Matcher Parser'


lombok {
  version = lombokVersion
  sha256 = ""
}


import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
  javaLauncher.set(javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(8)
  })

  ext.outputDir = file("$buildDir/delombok")
  outputs.dir(outputDir)

  sourceSets.main.java.srcDirs.each {
    inputs.dir(it)
    args(it, "-d", outputDir)
  }

  doFirst {
    outputDir.deleteDir();
  }
}


configurations {
  compile {
    extendsFrom = extendsFrom.findAll { it != configurations.antlr }
  }
}


def antlrVersion = "4.9.3"


apply from: file('../gradle/javadoc.gradle')
apply from: file('../gradle/quality.gradle')
apply from: file('../gradle/publishing.gradle')


dependencies {
  implementation project(':protocol-core')
  implementation "org.antlr:antlr4-runtime:${antlrVersion}"

  antlr "org.antlr:antlr4:${antlrVersion}"
}


generateGrammarSource {
  outputDirectory = file("${buildDir}/generated-src/antlr/main/de/sayayi/lib/protocol/matcher")
  arguments += [ '-package', 'de.sayayi.lib.protocol.matcher' ]
}


sourcesJar {
  dependsOn generateGrammarSource

  exclude "**/*.tokens"
  exclude "**/*.interp"
  exclude "**/*.g4"
  exclude "gen/**"
}


javadoc {
  exclude '**/*.tokens'
  exclude '**/*.interp'
}


clean {
  delete file("src/main/antlr/gen")
  delete file("src/main/gen")
  delete fileTree("src/main/antlr").matching {
    include "**/*.tokens"
  }
}